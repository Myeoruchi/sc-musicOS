import TriggerEditor.BGMPlayer as BGM;
import functions as fc;
import opening as op;
import musicPlay as mp;
import musicInfo as info;
import chatFunction as cf;

const cp = getuserplayerid();
var superUser;
var status;

function onPluginStart() {
    fc.speedBan();
    randomize();
    op.mouseInit();
    cf.variableInit();
    BGM.loadSound();
    SetMemoryEPD(EPD(0x5124F0), SetTo, 29);	// x2배속
    setcurpl(cp);
    BGM.SetBGM(info.musicNumMax);
}

function beforeTriggerExec() {
    setcurpl(cp);
    if (!playerexist(superUser)) { foreach (cp : EUDLoopPlayer("Human")) { superUser = cp; break; } }
    if (status) {
        if (status == 1) { cf.readChat(); }
        if (fc.getDeath(superUser, "Protoss Dragoon") && status != 4) {
            mp.autoSkip = !mp.autoSkip;
            if (mp.autoSkip) { simpleprint("\x1F■ \x04자동 스킵이 활성화 되어 모두가 맞혔을 때 자동으로 스킵됩니다."); }
            else { simpleprint("\x1F■ \x04자동 스킵이 비활성화 되어 자동으로 스킵되지 않습니다."); }
            PlayWAV("staredit\\wav\\0115.WAV");
        }
    }
    else { op.help(op.mouseCheck(superUser)); }
}

function afterTriggerExec() {
    switch (status) {
        case 0:
            BGM.Play();
            if (op.opening(superUser)) { status = 3; }
            break;
        case 1:
            BGM.Play();
            cf.writeChat(cp);
            cf.chatTypeUpdate();
            cf.allianceRefresh(cp);
            if (mp.musicSkip(superUser, cp)) { status = 2; }
            mp.musicText(cp, true);
            break;
        case 2:
            if (mp.musicDelay()) { status = 3; }
            mp.musicText(cp, false);
            break;
        case 3:
            status = mp.musicInit();
            break;
        case 4:
            const s = StringBuffer(2048);
            once {
                s.insert(0);
                s.appendf("\x13\x19┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓\n\n\n");
                s.appendf("\x13\x04엔딩곡 : ???\n");
                s.appendf("\x13\x04플레이 해주셔서 감사합니다.\n");
                s.appendf("\x13\x04제작자 : Myeoruchi (Heaven)\n\n\n");
                s.appendf("\x13\x19┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛");
                BGM.SetBGM(info.musicNumMax+1);
            }
            s.DisplayAt(0);
            BGM.Play();
            break;
    }
}