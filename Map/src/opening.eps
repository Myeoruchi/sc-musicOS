import functions as fc;
import musicPlay as mp;
import musicInfo as info;
import chatFunction as cf;
import BGM;

import build.maker;
import build.leaderboard;
import build.hint1;
import build.ansAllow;

const feature = [0, 1, 0, 0, 1]; // 0 is on, 1 is off
const category = EUDArray(info.categoryNum);
var click;
var timer;
var status;

function mouseClick();
function mouseInit() { EUDRegisterObjectToNamespace("click", click); }

function opening(superUser) {
    const s = StringBuffer(2048);
    static var musicNumTemp, musicNumReal = info.musicNumMax, info.musicNumMax;
    var txtUpdate;
    if (!status) { timer++; }

    once (timer == 12*3) {
        PlayWAV("opening1");
        s.insert(0);
        s.appendf("\x13\x19┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓\n\n\n");
        s.appendf("\x13\x11노래 맞히기 오픈소스\n");
        s.appendf("\x13\x04제작 : {:t}\n", maker);
        s.appendf("\x13\x04소스 : Myeoruchi (Heaven)\n\n\n");
        s.appendf("\x13\x19┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛");
    }
    once (timer == 12*18) {
        PlayWAV("opening2");
        s.insert(0);
        s.appendf("\x13\x19┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓\n\n\n");
        s.appendf("\x13\x04정답을 맞혀도 \x11타인에게 보이지 않습니다.\n");
        s.appendf("\x13\x04스킵 : 못맞힌 사람의 전원 투표, 모두 맞혔을 경우 과반수 투표, 강제 스킵\n", superUser);
        s.appendf("\x13\x04최대 3점에서 힌트 등장 시마다 획득 가능한 점수 1점 감점\n\n\n");
        s.appendf("\x13\x19┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛");
    }
    if (timer == 12*48) {
        once { status = 1; txtUpdate = true; }
        CenterView("Anywhere");
        const clicked = fc.getDeath(superUser, "Terran Marine");
        if (clicked) {
            if (clicked == 100) { PlayWAV("skip"); click = 0; timer++; return false; }
            PlayWAV("click");
            feature[clicked-1] = !feature[clicked-1];
            txtUpdate = true;
        }
        if (txtUpdate) {
            txtUpdate = false;
            s.insert(0);
            s.appendf("\x13\x19┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓\n");
            s.appendf("\x13{0:c}{0:n}\x04님이 설정합니다.\n", superUser);
            s.appendf("\x13\x19설정\x04은 \x1E마우스 클릭\x04으로 진행할 수 있습니다.\n");
            s.appendf("\x13\x07초록색\x04은 활성화이며, \x08붉은색\x04은 비활성화입니다.\n");
            s.appendf("\x13{:t}{:t}　　　{:t}채팅분리\n", feature[0] ? EPD(Db("\x08")) : EPD(Db("\x07")), ansAllow, feature[1] ? EPD(Db("\x08")) : EPD(Db("\x07")));
            s.appendf("\x13{:t}{:t}　　　{:t}초성힌트\n", feature[2] ? EPD(Db("\x08")) : EPD(Db("\x07")), hint1, feature[3] ? EPD(Db("\x08")) : EPD(Db("\x07")));
            s.appendf("\x13{:t}２초듣기　　　　　　　\n", feature[4] ? EPD(Db("\x08")) : EPD(Db("\x07")));
            s.appendf("\x13\x1D설정완료\n");
            s.appendf("\x13\x19┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛");
        }
    }
    else if (timer == 12*48+1) {
        once { status = 2; txtUpdate = true; }
        CenterView("Anywhere");
        const clicked = fc.getDeath(superUser, "Terran Marine");
        var index;
        if (clicked) {
            switch (clicked) {
                case 1: index = index ? index-1 : info.categoryNum-1; break;
                case 2: index = index == info.categoryNum-1 ? 0 : index+1; break;
                case 3:
                    category[index] = !category[index];
                    if (category[index]) {
                        musicNumTemp -= info.categoryInclude[index];
                        if (musicNumTemp < musicNumReal) { musicNumReal = musicNumTemp; }
                    }
                    else { musicNumTemp += info.categoryInclude[index]; }
                    break;
                case 4: if (musicNumReal) { musicNumReal--; } break;
                case 5: musicNumReal = musicNumReal > 5 ? musicNumReal-5 : 0; break;
                case 6: musicNumReal = musicNumReal > 10 ? musicNumReal-10 : 0; break;
                case 7: if (musicNumTemp - musicNumReal) { musicNumReal++; } break;
                case 8: musicNumReal = musicNumTemp - musicNumReal > 5 ? musicNumReal+5 : musicNumTemp; break;
                case 9: musicNumReal = musicNumTemp - musicNumReal > 10 ? musicNumReal+10 : musicNumTemp; break;
                case 100: PlayWAV("skip"); status = 0; click = 0; return false;
            }
            PlayWAV("click");
            txtUpdate = true;
        }
        if (txtUpdate) {
            txtUpdate = false;
            s.insert(0);
            s.appendf("\x13\x19┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓\n");
            s.appendf("\x13\x1f범주를 클릭\x04해서 제외할 범주를 선택하세요.\n", superUser);
            s.appendf("\x13\x04전체 \x19{}\x04곡 중에 \x19{}\x04곡을 포함시켰고, \x19{}\x04곡 재생됩니다.\n", info.musicNumMax, musicNumTemp, musicNumReal);
            s.appendf("\x13\x08－１０　－５　－１　　\x04곡 수 조절　　\x07＋１　＋５　＋１０\n");
            s.appendf("\x13\x19▲\n");
            s.appendf("\x13{:t}{:t}\n", category[index] ? EPD(Db("\x08")) : EPD(Db("\x07")), info.categoryName[index]);
            s.appendf("\x13\x19▼\n");
            s.appendf("\x13\x1D설정완료\n");
            s.appendf("\x13\x19┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛");
        }
        eprintf("곡 수 조절기와 범주는 별개의 항목입니다.");
    }
    once (timer == 12*48+2) {
        foreach (i : py_range(info.categoryNum)) { info.categoryActive[i] = category[i]; }
        mp.musicNum = musicNumReal;
        cf.ansOption = !feature[0];
        mp.chatSeparate = !feature[1];
        mp.hint1 = !feature[2];
        mp.hint2 = !feature[3];
        mp.orderInit();
        s.insert(0);
        s.appendf("\x13\x19┏━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┓\n\n\n");
        s.appendf("\x13\x1F설정\x04이 전부 끝났습니다. 잠시 후, 게임이 시작됩니다.\n");
        s.appendf("\x13\x1D띄어쓰기와 대소문자\x04는 상관 없습니다.\n");
        s.appendf("\x13\x04강제 스킵 : \x19\"P\"\x04, 자동 스킵 : \x19\"Insert\"\x04 ({0:c}{0:n}\x04)\n\n\n", superUser);
        s.appendf("\x13\x19┗━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━┛");
        eprintf(" ");
    }
    once (timer == 12*68) {
        BGM.oneSec = !feature[4];
        LeaderBoardScore(Custom, "\x1E"+leaderboard);
        LeaderBoardComputerPlayers(Disable);
        println("\n\n\n\n\n\n\n\n\n\n ");
        return true;
    }
    s.DisplayAt(0);
    return false;
}

function help(pos) {
    if (status != 1) { return; }
    const text = [EPD(Db("제작자가 설정한 추가 정답을 허용합니다.")), EPD(Db("정답을 맞힌 사람의 채팅을 안보이게 합니다.")), EPD(Db("첫번째 힌트가 공개됩니다.")), EPD(Db("초성 힌트가 공개됩니다.")), EPD(Db("맞히기 전까지 음원을 2초만 재생합니다."))];
    const s = StringBuffer(1024);
    s.insert(0);
    if (pos) { s.appendf("\x1D( \x04활성화 할 경우 {:t} \x1D)", text[pos-1]); }
    else { s.appendf(" "); }
    eprintf("{:t}", s.epd);
}

function mouseCheck(superUser) {
    if (!status) { return 0; }
    const mouseX = dwread_epd(EPD(0x62848C)) + dwread_epd(EPD(0x6CDDC4));
    const mouseY = dwread_epd(EPD(0x6284A8)) + dwread_epd(EPD(0x6CDDC8));
    var pos = 0;

    if (status == 1) {
        if (4036 <= mouseX && mouseX <= 4082) {
            if (4081 <= mouseY && mouseY < 4096) { pos = 1; }
            else if (4096 <= mouseY && mouseY < 4111) { pos = 3; }
            else if (4111 <= mouseY && mouseY < 4126) { pos = 5; }
        }
        else if (4111 <= mouseX && mouseX <= 4157) {
            if (4081 <= mouseY && mouseY < 4096) { pos = 2; }
            else if (4096 <= mouseY && mouseY < 4111) { pos = 4; }
        }
    }
    else if (status == 2) {
        if (4072 <= mouseX && mouseX <= 4118) {
            if (4080 <= mouseY && mouseY < 4094) { pos = 1; }
            else if (4112 <= mouseY && mouseY < 4126) { pos = 2; }
            else if (4094 <= mouseY && mouseY < 4112) { pos = 3; }
        }
        else if (4067 <= mouseY && mouseY <= 4075) {
            if (4031 <= mouseX && mouseX <= 4045) { pos = 4; }
            else if (3999 <= mouseX && mouseX <= 4015) { pos = 5; }
            else if (3956 <= mouseX && mouseX <= 3982) { pos = 6; }
            else if (4145 <= mouseX && mouseX <= 4160) { pos = 7; }
            else if (4178 <= mouseX && mouseX <= 4195) { pos = 8; }
            else if (4210 <= mouseX && mouseX <= 4238) { pos = 9; }
        }
    }
    if (getuserplayerid() == superUser && mouseClick(superUser)) {
        if (4072 <= mouseX && mouseX <= 4118 && 4128 <= mouseY && mouseY <= 4143) { click = 100; }
        else { click = pos; }
    }
    else { click = 0; }
    return pos;
}

function mouseClick(superUser) {
    var press;
    if (dwread_epd(EPD(0x6CDDC0))) {
        if (press) { return false; }
        press = true;
        return true;
    }
    else { press = false; }
    return false;
}